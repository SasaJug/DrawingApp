apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionCode 3
        versionName "2.2"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        def props = new Properties()
        def File configFile = file("$rootDir/config/amazon.properties")
        if (configFile.exists() && configFile.canRead()) {
            props.load(new FileInputStream(configFile))
            props.each { key, val ->
                String[] value = val.split("#")
                buildConfigField value[0], key, value[1]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJavaVersion"

    //Room
    implementation 'androidx.room:room-runtime:2.0.0'
    implementation 'androidx.room:room-rxjava2:2.0.0'
    kapt 'androidx.room:room-compiler:2.0.0'

    //aws
    implementation "com.amazonaws:aws-android-sdk-core:$rootProject.ext.awsVersion"
    implementation "com.amazonaws:aws-android-sdk-cognitoidentityprovider:$rootProject.ext.awsVersion"
    implementation "com.amazonaws:aws-android-sdk-s3:$rootProject.ext.awsVersion"
    implementation "com.amazonaws:aws-android-sdk-ddb:$rootProject.ext.awsVersion"

    //Mockito
    testImplementation "org.mockito:mockito-core:$rootProject.MockitoVersion"

    //JUnit
    testImplementation "junit:junit:$rootProject.JUnitVersion"

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'


    implementation project(':domain')
}
repositories {
    mavenCentral()
}
